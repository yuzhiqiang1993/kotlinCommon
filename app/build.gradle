apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'walle'
apply plugin: 'AndResGuard'
apply plugin: "com.github.ben-manes.versions"
android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version
    defaultConfig {
        applicationId "com.yzq.kotlincommon"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode version_code
        versionName version_name
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        resConfigs "zh"
        ndk {
            abiFilters "armeabi-v7a"
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {

        release {
            storePassword rootProject.ext.signConfigs.storePwd
            keyAlias rootProject.ext.signConfigs.keyAlias
            keyPassword rootProject.ext.signConfigs.keypwd
            storeFile file(rootProject.ext.signConfigs.storeFilePath)
            v2SigningEnabled true
        }
    }

    buildTypes {

        debug {
            signingConfig signingConfigs.release
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    kapt "com.alibaba:arouter-compiler:$arouter_compiler"
    implementation "com.github.yuzhiqiang1993:zxing:${zxing_version}"
    implementation "org.jsoup:jsoup:${jsoup_version}"
    implementation "com.meituan.android.walle:library:${walleVersion}"
    kapt "androidx.room:room-compiler:$room_version"

    implementation 'com.shuyu:GSYVideoPlayer:7.1.2'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    implementation 'com.github.zhpanvip:BannerViewPager:2.6.6'
    implementation 'com.rishabhharit.roundedimageview:RoundedImageView:0.8.4'

    implementation project(':common')
    if (isModule.toBoolean()) {
        implementation project(':gao_de_map')
    }


}


walle {
    apkOutputFolder = new File("${project.buildDir}/outputs/channels")
    apkFileNameFormat = '${projectName}-${channel}-${buildType}-v${versionName}-${versionCode}-${buildTime}.apk'
    channelFile = new File("${project.getProjectDir()}/channel")
}


andResGuard {
    // mappingFile = file("./resource_mapping.txt")
    mappingFile = null
    use7zip = true
    useSign = true
    // 打开这个开关，会keep住所有资源的原始路径，只混淆资源的名字
    keepRoot = false
    whiteList = [
            // for your icon
            "R.drawable.icon",
            // for fabric
            "R.string.com.crashlytics.*",
            // for google-services
            "R.string.google_app_id",
            "R.string.gcm_defaultSenderId",
            "R.string.default_web_client_id",
            "R.string.ga_trackingId",
            "R.string.firebase_database_url",
            "R.string.google_api_key",
            "R.string.google_crash_reporting_api_key"
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
    ]
    sevenzip {
        artifact = "com.tencent.mm:SevenZip:$andResGuardVersion"
    }
}


afterEvaluate {
    tasks.getByName("assembleReleaseChannels")
            .dependsOn(resguardRelease)

}
