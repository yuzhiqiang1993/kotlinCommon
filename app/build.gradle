plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}


apply from: file("plugins/bytex.gradle")

android {

    compileSdk compile_sdk_version
    defaultConfig {
        applicationId "com.yzq.kotlincommon"
        minSdk min_sdk_version
        targetSdk target_sdk_version
        versionCode version_code
        versionName version_name
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        resConfigs "zh"
        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a"
        }
    }

    signingConfigs {

        release {
            storePassword rootProject.ext.signConfigs.storePwd
            keyAlias rootProject.ext.signConfigs.keyAlias
            keyPassword rootProject.ext.signConfigs.keypwd
            storeFile file(rootProject.ext.signConfigs.storeFilePath)
            v2SigningEnabled true
        }
    }

    buildTypes {

        debug {
            signingConfig signingConfigs.release
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "channel"
    productFlavors {

        dev{
            manifestPlaceholders = [
                    "APP_NAME"          : "KotlinCommon-dev",
                    "META_CHANNEL_VALUE": "dev"
            ]
        }

        wandoujia {
            manifestPlaceholders = [
                    "APP_NAME"          : "KotlinCommon-wandoujia",
                    "META_CHANNEL_VALUE": "wandoujia"
            ]
        }
        yingyongbao {
            manifestPlaceholders = [
                    "APP_NAME"          : "KotlinCommon-yingyongbao",
                    "META_CHANNEL_VALUE": "yingyongbao"
            ]
        }

        applicationVariants.all { variant ->
            variant.outputs.all { output ->

                def date = new Date().format("yyyy-MM-dd_HH-mm", TimeZone.getTimeZone("GMT+8"))

                outputFileName = "${defaultConfig.applicationId}-${variant.productFlavors[0].name}_${defaultConfig.versionName}-${date}.apk"
            }
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }


}


kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    kapt "com.alibaba:arouter-compiler:$arouter_compiler"
    implementation "com.github.yuzhiqiang1993:zxing:${zxing_version}"
    implementation "org.jsoup:jsoup:${jsoup_version}"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "androidx.viewpager2:viewpager2:${viewpager2}"

    implementation "com.github.zhpanvip:BannerViewPager:${bannerViewPager}"

    implementation project(':common')
    implementation "io.github.yuzhiqiang1993:xeon_bsdiff:1.0.1"
    if (isModule.toBoolean()) {
        implementation project(':gao_de_map')
    }


}


task testTask() {


    doFirst {
        println "testTask执行 doFirst"
    }

    doLast {
        println "testTask执行 doLast"
    }

}

/*自定义一个task*/
task testTask1() {
    dependsOn testTask
    doLast {
        println("testTask1执行")
    }


}

/*project表示当前module的项目*/
println "我是App工程，${project.name},我的路径是:${project.projectDir}"

